#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT([PSRCHIVE],[6.0],[psrchive@sourceforge.net])

AC_CONFIG_AUX_DIR([config])
AC_CONFIG_SRCDIR([Util/Makefile.am])

AC_ARG_VAR([PSRHOME],[Standard pulsar home directory])
AC_ARG_VAR([LOGIN_ARCH],[Architecture-dependent sub-directory of PSRHOME])
AC_PREFIX_DEFAULT([${PSRHOME:-"/usr/local"}${PSRHOME:+"/$LOGIN_ARCH"}])

if test -z "$PSRHOME"; then
  AC_MSG_WARN([The PSRHOME environment variable is not set])
else
  if test -z "$LOGIN_ARCH"; then
    AC_MSG_WARN([The LOGIN_ARCH environment variable is not set])
  fi
fi

# Check if the user has set compiler options
SWIN_OPTIONS_SET

# Initialize automake to place object files in sub-directories
AM_INIT_AUTOMAKE([subdir-objects])

# Create static libraries by default
AC_DISABLE_SHARED

# Enable large-file support
AC_SYS_LARGEFILE

# Determine the machine endian
AC_C_BIGENDIAN

# Ensure that SIGPROC find does not over-ride the standard
SWIN_PROG_FIND

# Check which version of the pulsar catalog is installed
SWIN_PSRCAT

# Enable convenience compiler selection
SWIN_COMPILER

# Checks for programs.
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_CC
AC_PROG_CPP
AM_PROG_CC_C_O
AC_PROG_INSTALL
AC_PROG_LIBTOOL
AC_PROG_FC
AC_F77_WRAPPERS
_STAR_RESTFP_FIX

#
# Disable the debugging information option, -g
#
SWIN_DEBUG

#
# Check C++ template compilation
#
SWIN_BEST_PARTIAL_SPECIALIZATION
SWIN_DEFAULT_PARTIAL_SPECIALIZATION
SWIN_COMPLEX_TEMPLATE

#
# Set up for standard pulsar installation
#
CPPFLAGS="-I/usr/local/include $CPPFLAGS"
LDFLAGS="-L/usr/local/lib $LDFLAGS"

if test "$PSRHOME"; then
 CPPFLAGS="-I$PSRHOME/packages/$LOGIN_ARCH/include $CPPFLAGS"
 LDFLAGS="-L$PSRHOME/packages/$LOGIN_ARCH/lib $LDFLAGS"
fi

#
# Checks for header files.
#
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([sys/statvfs.h sys/vfs.h sys/mount.h])

#
# Checks for essential libraries
#
AC_CHECK_LIB([m], [sin])
SWIN_LIB_SLA([],AC_MSG_WARN([SLALIB Positional Astronomy Library not found.
PSRCHIVE will compile SLALIB code in Util/third/star.]))
SWIN_LIB_FFTW
SWIN_LIB_MKL
SWIN_LIB_IPP

#
# Check that at least one FFT library is available
#
if test x"$have_fftw" = xno -a x"$have_fftw3" = xno -a x"$have_mkl" = xno; then
  AC_MSG_ERROR([No suitable FFT library was found.

  Please see http://psrchive.sourceforge.net/third/fftw

])
fi

AC_SUBST(FFT_LIBS)
FFT_LIBS="$MKL_LIBS $FFTW_LIBS $FFTW3_LIBS $IPP_LIBS"
AC_SUBST(FFT_CFLAGS)
FFT_CFLAGS="$MKL_CFLAGS $FFTW_CFLAGS $FFTW3_CFLAGS $IPP_CFLAGS"

#
# Checks for optional libraries
#
SWIN_LIB_CFITSIO
SWIN_LIB_MPI
SWIN_LIB_READLINE
SWIN_LIB_PTHREAD

if test x"$enable_lean" == xyes; then

AM_CONDITIONAL(HAVE_PGPLOT, [test 1 = 0])
AM_CONDITIONAL(HAVE_QT, [test 1 = 0])
AM_CONDITIONAL(HAVE_QTDRIV, [test 1 = 0])

else

#
# Checks for graphics libraries
#
SWIN_LIB_PGPLOT
BNV_HAVE_QT
SWIN_LIB_QTDRIV

fi

#
# For developers: preserve file modification times
#
INSTALL_DATA="${INSTALL} -m 644 -p"
install_sh="CPPROG='cp -p' ${install_sh}"

#
# For developers: provide the ability to compile a lean version of the code
#
AC_ARG_WITH([lean], AC_HELP_STRING([--enable-lean],
				   [compile a lean version of the library]))

AM_CONDITIONAL(MAKE_LEAN, [test x"$enable_lean" == xyes])

#
# Checks for library functions.
#
SWIN_FUNC_GETOPT_LONG

AM_CONFIG_HEADER([config.h])
AC_CONFIG_FILES([Util/Makefile
		Util/third/Makefile
		Util/third/parsifal/Makefile
		Util/third/taygeta/Makefile
		Util/third/star/Makefile
		Util/units/Makefile
		Util/fft/Makefile
		Util/genutil/Makefile
		Util/pgutil/Makefile
		Util/qtutil/Makefile
		Util/fitsutil/Makefile
		Util/tempo/Makefile
		Base/Makefile
		Base/Classes/Makefile
		Base/Extensions/Makefile
		Base/Checks/Makefile
		Base/Formats/Makefile
		Base/Formats/EPN/Makefile
		Base/Formats/Timer/Makefile
		Base/Formats/PSRFITS/Makefile
                Base/Formats/ASCII/Makefile
		Base/Applications/Makefile
		More/Makefile
		More/General/Makefile
		More/Plotting/Makefile
		More/MEAL/Makefile
		More/Timing/Makefile
		More/Polarimetry/Makefile
		More/Polarimetry/iri2001/Makefile
		More/rhythm/Makefile
		More/Applications/Makefile
		Makefile
		psrchive_ldflags
		psrchive_cflags])

AC_OUTPUT

echo
echo

if test x"$enable_lean" == xyes; then

   echo "********************************************"
   echo "A lean version of PSRCHIVE will be compiled."
   echo "This means that the polarimetric calibration"
   echo "software, related error analysis routines,"
   echo "and graphical interfaces will be unavailable."
   echo "********************************************"

else

if test x"$have_cfitsio" != xyes; then
   echo "The CFITSIO library was not found:"
   echo "  PSRCHIVE applications will not be able to load PSRFITS files."
fi

if test x"$have_pgplot" != xyes; then
   echo "The PGPLOT library was not found:"
   echo "  PSRCHIVE applications will not be able to plot."
   echo "  Please see http://psrchive.sourceforge.net/third/pgplot"
fi

if test x"$have_qt" != xyes; then
   echo "The Qt library was not found:"
   echo "  PSRCHIVE graphical user interfaces will not be compiled."
fi

if test x"$have_qtdriv" != xyes; then
   echo "The qtdriv device handler is not installed:"
   echo "  rhythm will not be compiled."
fi

fi

echo
echo
